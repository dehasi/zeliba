plugins {
    id 'java'
    id 'maven-publish'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'

group 'me.dehasi'
archivesBaseName = 'zeliba'
version 'LATEST-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

signing {
    sign publishing.publications.mavenJava
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
check.dependsOn jacocoTestReport

publishing {
    publications{

        mavenJava(MavenPublication){
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            groupId = 'me.dehasi'
            artifactId = 'zeliba'
            version = version

            pom {
                name = 'ZeLiba'
                description = 'DSL-ish helper to make the java comparison more readable'
                url =  'https://github.com/dehasi/zeliba'
                inceptionYear = '2020'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dehasi'
                        name = 'Ravil'
                        email = 'dehasi@protonmail.com'
                    }
                }
                scm {
                    connection= 'scm:git:git://github.com/dehasi/zeliba.git'
                    developerConnection='scm:git:ssh://github.com:dehasi/zeliba.git'
                    url='https://github.com/dehasi/zeliba'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
    }
}

