plugins {
    id 'java'
}

apply plugin: 'maven'
apply plugin: 'signing'

group 'me.dehasi'
archivesBaseName = 'zeliba'
version '2020.02.20-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    //sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ZeLiba'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'DSL-ish helper to make the java comparison more readable'
                url 'https://github.com/dehasi/zeliba'

                scm {
                    connection 'scm:git:git://github.com/dehasi/zeliba.git'
                    developerConnection 'scm:git:ssh://github.com:dehasi/zeliba.git'
                    url 'http://github.com/dehasi/zeliba/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dehasi'
                        name 'Ravil'
                        email 'dehasi@dehasi.me'
                    }
                }
            }
        }
    }
}
